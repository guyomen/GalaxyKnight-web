<?xml version='1.0' encoding='UTF-8'?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method="html" encoding="utf-8" indent="yes" />

<xsl:template match="/">
 <html>
  <head>
    <title>GK Resources</title>
    <style type="text/css">
      th {background-color:silver;}
      td {border-style:solid; border-width:1px;}
    </style>
  </head>
  <body>
	<h1>Galaxy Knight</h1>
	<a href="GkRace.xml">Les Races</a><br/>
	<a href="GkCiv.xml">Les Civilisations</a><br/>
	<a href="GkEthnie.xml">Les Ethnies</a><br/>
	<a href="GkCore.xml">Les caracteristiques</a><br/>
	<a href="GkRegle.xml">Les Regles</a><br/>
    <xsl:apply-templates/>
  </body>
</html>
</xsl:template>

<!-- Generate Race list  -->
<xsl:template match="GkRace">
  <H2>Races</H2>
  <xsl:variable name="nbOfItem"><xsl:value-of select="itemsInfo/itemsList/@nb"/></xsl:variable>
  <xsl:variable name="nbOfItemPlus"><xsl:value-of select="$nbOfItem+1"/></xsl:variable>
  <table>
    <tr> 
		<th rowspan="2">Race</th>
		<th rowspan="2">Description race</th>
		<th rowspan="2">bonus a la création</th>
		<th colspan="{$nbOfItemPlus}"><xsl:value-of select="itemsInfo/Nom"/></th>
		<th rowspan="2"><xsl:value-of select="Races/Race/items[1]/@name"/></th>
		<th rowspan="2"><xsl:value-of select="Races/Race/items[2]/@name"/></th>
	</tr>
	<tr>
    <xsl:for-each select="itemsInfo/itemsList/item">	
		<th width="30"><xsl:value-of select="@id"/></th>
    </xsl:for-each>
		<th width="30">Total</th>
	</tr>
    <xsl:for-each select="Races/Race">
		<xsl:sort select="@id"/>
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
</xsl:template>

<!-- Generate Civ list  -->
<xsl:template match="GkCiv">
  <H2>Civilisations</H2>
  <xsl:variable name="nbOfItem"><xsl:value-of select="itemsInfo/itemsList/@nb"/></xsl:variable>
  <xsl:variable name="nbOfItemPlus"><xsl:value-of select="$nbOfItem+1"/></xsl:variable>
  <table>
    <tr> 
		<th rowspan="2">Civilisation</th>
		<th rowspan="2">Informations générales</th>
		<th rowspan="2">Information citoyen</th>
		<th colspan="{$nbOfItemPlus}"><xsl:value-of select="itemsInfo/Nom"/></th>
		<th rowspan="2"><xsl:value-of select="Civilisations/Civilisation/items/@name"/></th>
	</tr>
	<tr>
    <xsl:for-each select="itemsInfo/itemsList/item">	
		<th width="30"><xsl:value-of select="@id"/></th>
    </xsl:for-each>
		<th width="30">Total</th>
	</tr>
    <xsl:for-each select="Civilisations/Civilisation">
		<xsl:sort select="@id"/>
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
</xsl:template>

<!-- Generate Ethnie list  -->
<xsl:template match="GkEthnie">
  <H2>Ethnies</H2>
  <table>
    <tr> 
		<th>Ethnie</th>
		<th>Race</th>
		<th>Civilisation</th>
		<th>Attribut</th>
	</tr>
    <xsl:for-each select="Ethnies/Ethnie">
		<xsl:sort select="@id"/>
		<xsl:call-template name="Link"/>
    </xsl:for-each>
  </table>
</xsl:template>

<!-- Generate Core list  -->
<xsl:template match="GkCore">
  <H2>Caracteristiques personnage</H2>
  <xsl:variable name="nbOfItem"><xsl:value-of select="itemsInfo/itemsList/@nb"/></xsl:variable>
  <table>
    <tr> 
		<th rowspan="2">Caractéristiques</th>
		<th rowspan="2">Description</th>
		<th colspan="{$nbOfItem}"><xsl:value-of select="itemsInfo/Nom"/></th>
	</tr>
	<tr>
    <xsl:for-each select="itemsInfo/itemsList/item">	
		<th><xsl:value-of select="@id"/></th>
    </xsl:for-each>	
	</tr>
    <xsl:for-each select="Caracteristiques/Caracteristique">
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
  <H2>Competences personnage</H2>
  <table>
    <xsl:for-each select="Competences/Competence">	
		<tr>
			<th colspan="4"><xsl:value-of select="Nom"/></th>
		</tr>
		<tr>
			<th>Info</th>
			<td colspan="3"><xsl:value-of select="Description"/></td>
		</tr>
		<tr>
			<th>Attribut</th>
			<th>Type</th>
			<th>Groupe</th>
			<th>Carac Alt</th>
			<th>Info</th>
			<th>Connaissances</th>
		</tr>
		<xsl:call-template name="Attribut"/>
		<tr>
			<br></br>
		</tr>
    </xsl:for-each>
  </table>
</xsl:template>

<!-- Generate Core list  -->
<xsl:template match="GkRegle">
  <H2>Creation personnage</H2>
  <table>
    <tr> 
		<th rowspan="1">Etape</th>
		<th rowspan="1">Description</th>	
		<th rowspan="1">Explication</th>		
	</tr>
    <xsl:for-each select="Creation/Regle">
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
    <H2>Experience</H2>
  <table>
    <tr> 
		<th rowspan="1">Etape</th>
		<th rowspan="1">Description</th>	
		<th rowspan="1">Explication</th>		
	</tr>
    <xsl:for-each select="Experience/Regle">
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
    <H2>Regle de jeu</H2>
  <table>
    <tr> 
		<th rowspan="1">Nom</th>
		<th rowspan="1">Description</th>	
		<th rowspan="1">Explication</th>		
	</tr>
    <xsl:for-each select="Jeu/Regle">
		<xsl:call-template name="Object"/>
    </xsl:for-each>
  </table>
</xsl:template>
  
<!-- Template -->
<xsl:template name="Object">
	<tr> 
		<th><xsl:value-of select="Nom"/></th>	
		<xsl:for-each select="Description">
			<td><xsl:call-template name="BigText"/></td>
		</xsl:for-each>
		<xsl:for-each select="Information">
			<td><xsl:call-template name="BigText"/></td>
		</xsl:for-each>
		<xsl:if test="itemsID">
			<xsl:call-template name="itemsID"/>			
			<td align="center"><xsl:value-of select="sum(itemsID/item)"/></td>
		</xsl:if> 
		<xsl:for-each select="items">		
			<td><xsl:call-template name="items"/></td>
		</xsl:for-each> 
	</tr>
</xsl:template>

<xsl:template name="itemsID">
    <xsl:for-each select="itemsID/item">	
		<td align="center"><xsl:value-of select="."/></td>
    </xsl:for-each>
</xsl:template>

<xsl:template name="items">
	<xsl:for-each select="item">	
		<xsl:value-of select="."/><br></br>
	</xsl:for-each>
</xsl:template>

<xsl:template name="Link">
	<tr>
	<th><xsl:value-of select="Nom"/></th>
    <xsl:for-each select="Link">	
		<td><xsl:value-of select="."/></td>
    </xsl:for-each>
	<xsl:for-each select="items">		
		<td><xsl:call-template name="items"/></td>
    </xsl:for-each>
    </tr>
</xsl:template>

<xsl:template name="Attribut">
	<xsl:for-each select="Attributs/Attribut">
		<xsl:variable name="typeImg"><xsl:value-of select="type"/></xsl:variable>
		<tr>
			<td><xsl:value-of select="Nom"/></td>
			<td><img src="image/{$typeImg}.svg" alt="{$typeImg}"/></td>
			<td><xsl:value-of select="group"/></td>
			<td><xsl:value-of select="caracAlt"/></td>
			<xsl:for-each select="Description">
				<td><xsl:call-template name="BigText"/></td>
			</xsl:for-each>
			<td>
			<xsl:for-each select="items">		
				<xsl:call-template name="items"/>
			</xsl:for-each>
			</td>			
		</tr>
	</xsl:for-each>
</xsl:template>

<xsl:template name="BigText">
	<xsl:for-each select="line">
			<xsl:value-of select="."/><br></br>
	</xsl:for-each>
</xsl:template>			

</xsl:stylesheet>
